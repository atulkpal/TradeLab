// import com.google.gms.googleservices.GoogleServicesPlugin.MissingGoogleServicesStrategy
import java.util.Properties

plugins {
  alias(libs.plugins.android.application)
  alias(libs.plugins.kotlin.android)
  alias(libs.plugins.kotlin.compose)
  alias(libs.plugins.google.devtools.ksp)
  alias(libs.plugins.roborazzi)
  alias(libs.plugins.secrets)
  alias(libs.plugins.google.services)
  id("com.google.firebase.crashlytics")
  alias(libs.plugins.hilt)
}

android {
  namespace = "com.ashwathai.tradelab"
  compileSdk = 37

  val keystorePropertiesFile = file("keystore.properties")
  val keystoreProperties = Properties()
  val isReleaseTask = gradle.startParameter.taskNames.any { it.contains("Release", ignoreCase = true) }
  if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(keystorePropertiesFile.inputStream())
  }

  defaultConfig {
    applicationId = "com.ashwathai.tradelab"
    minSdk = 24
    targetSdk = 37
    versionCode = 14
    versionName = "2.2.1"

    testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    create("release") {
      if (keystorePropertiesFile.exists()) {
        val storeFilePath = keystoreProperties.getProperty("storeFile")
        val storePass = keystoreProperties.getProperty("storePassword")
        val alias = keystoreProperties.getProperty("keyAlias")
        val keyPass = keystoreProperties.getProperty("keyPassword")
        if (isReleaseTask && listOf(storeFilePath, storePass, alias, keyPass).any { it.isNullOrBlank() }) {
          error("Release signing requires storeFile, storePassword, keyAlias, and keyPassword in keystore.properties")
        }
        if (!storeFilePath.isNullOrBlank()) storeFile = file(storeFilePath)
        storePassword = storePass
        keyAlias = alias
        keyPassword = keyPass
      } else {
        val keystorePath = System.getenv("KEYSTORE_PATH")
        val storePass = System.getenv("STORE_PASSWORD")
        val alias = System.getenv("KEY_ALIAS") ?: "upload"
        val keyPass = System.getenv("KEY_PASSWORD")
        if (isReleaseTask && listOf(keystorePath, storePass, alias, keyPass).any { it.isNullOrBlank() }) {
          error("Release signing requires KEYSTORE_PATH, STORE_PASSWORD, KEY_ALIAS, and KEY_PASSWORD environment variables")
        }
        if (!keystorePath.isNullOrBlank()) storeFile = file(keystorePath)
        storePassword = storePass
        keyAlias = alias
        keyPassword = keyPass
      }
    }
    create("debugConfig") {
      val localDebugKeystore = file("${rootDir}/debug.keystore")
      val defaultDebugKeystore = file(System.getProperty("user.home") + "/.android/debug.keystore")
      storeFile = if (localDebugKeystore.exists()) localDebugKeystore else defaultDebugKeystore
      storePassword = "android"
      keyAlias = "androiddebugkey"
      keyPassword = "android"
    }
  }

  buildTypes {
    release {
      isCrunchPngs = false
      isMinifyEnabled = true
      isShrinkResources = true
      proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro")
      signingConfig = signingConfigs.getByName("release")
    }
    debug {
      signingConfig = signingConfigs.getByName("debugConfig")
    }
  }
  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
  kotlinOptions {
    jvmTarget = "11"
  }
  buildFeatures {
    compose = true
    buildConfig = true
  }
  testOptions { unitTests { isIncludeAndroidResources = true } }
}

// Configure the Secrets Gradle Plugin to use .env and .env.example files
// to match the convention used in Web projects.
secrets {
  propertiesFileName = ".env"
  defaultPropertiesFileName = ".env.example"
}

// googleServices { missingGoogleServicesStrategy = MissingGoogleServicesStrategy.WARN }

// Some unused dependencies are commented out below instead of being removed.
// This makes it easy to add them back in the future if needed.
dependencies {
  implementation(platform(libs.androidx.compose.bom))
  implementation(platform(libs.firebase.bom))
  implementation("com.google.firebase:firebase-analytics")
  implementation("com.google.firebase:firebase-crashlytics")
  implementation("com.google.firebase:firebase-messaging")
  // implementation(libs.accompanist.permissions)
  implementation(libs.androidx.activity.compose)
  // implementation(libs.androidx.camera.camera2)
  // implementation(libs.androidx.camera.core)
  // implementation(libs.androidx.camera.lifecycle)
  // implementation(libs.androidx.camera.view)
  implementation(libs.androidx.compose.material.icons.core)
  implementation(libs.androidx.compose.material.icons.extended)
  implementation(libs.androidx.compose.material3)
  implementation(libs.androidx.compose.ui)
  implementation(libs.androidx.compose.ui.graphics)
  implementation(libs.androidx.compose.ui.tooling.preview)
  implementation(libs.androidx.core.ktx)
  implementation(libs.androidx.work.runtime)
  // implementation(libs.androidx.datastore.preferences)
  implementation(libs.androidx.lifecycle.runtime.compose)
  implementation(libs.androidx.lifecycle.runtime.ktx)
  implementation(libs.androidx.lifecycle.process)
  implementation(libs.androidx.lifecycle.viewmodel.compose)
  // implementation(libs.androidx.navigation.compose)
  implementation(libs.androidx.room.ktx)
  implementation(libs.androidx.room.runtime)
  // implementation(libs.coil.compose)
  implementation(libs.converter.moshi)
  // implementation(libs.firebase.ai)
  // Uncomment to use Firestore:
  implementation(libs.firebase.firestore)
  implementation(libs.firebase.storage)

  // Firebase Auth with Google Sign-In requires all of the following to be uncommented together.
  // If you are using Firebase Auth with other providers (e.g. Email/Password), you may only need
  // firebase-auth.
  implementation(libs.firebase.auth)
  implementation(libs.androidx.credentials)
  implementation(libs.androidx.credentials.play.services)
  implementation(libs.googleid)
  implementation(libs.firebase.appcheck.playintegrity)
  implementation(libs.kotlinx.coroutines.android)
  implementation(libs.kotlinx.coroutines.core)
  implementation(libs.logging.interceptor)
  implementation(libs.moshi.kotlin)
  implementation(libs.okhttp)
  // implementation(libs.play.services.location)
  implementation(libs.retrofit)
  implementation(libs.levelplay.sdk)
  implementation(libs.unityads.adapter)
  implementation(libs.unity.ads) // Unity Ads SDK - the adapter does NOT bundle it (NoClassDefFoundError without this)
implementation(libs.play.services.appset)
implementation(libs.play.services.ads.identifier)
implementation(libs.play.services.basement)
  implementation(libs.billing.ktx)
  implementation(libs.app.update.ktx)

  implementation(libs.hilt.android)
  "ksp"(libs.hilt.compiler)
  implementation(libs.hilt.navigation.compose)

  implementation(project(":shared"))
  testImplementation(libs.androidx.compose.ui.test.junit4)
  testImplementation(libs.androidx.core)
  testImplementation(libs.androidx.junit)
  testImplementation(libs.junit)
  testImplementation(libs.kotlinx.coroutines.test)
  testImplementation(libs.robolectric)
  testImplementation(libs.roborazzi)
  testImplementation(libs.roborazzi.compose)
  testImplementation(libs.roborazzi.junit.rule)
  testImplementation(libs.mockk)
  testImplementation(libs.turbine)
  androidTestImplementation(platform(libs.androidx.compose.bom))
  androidTestImplementation(libs.androidx.compose.ui.test.junit4)
  androidTestImplementation(libs.androidx.espresso.core)
  androidTestImplementation(libs.androidx.junit)
  androidTestImplementation(libs.androidx.runner)
  debugImplementation(libs.androidx.compose.ui.test.manifest)
  debugImplementation(libs.androidx.compose.ui.tooling)
  "ksp"(libs.androidx.room.compiler)
  "ksp"(libs.moshi.kotlin.codegen)
}
